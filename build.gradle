import groovy.text.SimpleTemplateEngine

ext {
    coreName = project.getName()
    mainVersion = "1.0.0"
    mainGroup = "teck.geekcity.athena"
    javaVersion = 1.8
    mavenRepositoryUrlList = [
            "https://maven.aliyun.com/repository/public",
            "https://maven.aliyun.com/repository/spring",
            "https://maven.aliyun.com/repository/mapr-public",
            "https://maven.aliyun.com/repository/spring-plugin",
            "https://maven.aliyun.com/repository/gradle-plugin",
            "https://maven.aliyun.com/repository/google",
            "https://maven.aliyun.com/repository/jcenter",
    ]
    gitCommitId = { ->
        def headDeafOutputStream = new ByteArrayOutputStream()
        exec {
            commandLine("git", "diff", "HEAD")
            standardOutput = headDeafOutputStream
        }
        def headDeaf = headDeafOutputStream.toString().trim()
        def clean = headDeaf.isEmpty()

        def commitIdOutputStream = new ByteArrayOutputStream()
        exec {
            commandLine("git", "rev-parse", "--short", "HEAD")
            standardOutput = commitIdOutputStream
        }
        return "${commitIdOutputStream.toString().trim()}${clean ? "" : ".dirty"}"
    }
    constructGroupName = { middleName, projectName ->
        return "${ext.mainGroup}.${middleName}.${projectName} "
    }
    constructVersion = { ->
        return "${ext.mainVersion}-C${ext.gitCommitId()}"
    }
    jarDependencyVersion = { groupWithArtifact ->
        def slf4jVersion = "1.7.15"
        def jacksonVersion = "2.9.8"
        def grpcVersion = "1.30.1"
        def dockerJavaVersion = "3.2.7"
        def junitJupiterVersion = "5.3.1"
        def minaSshVersion = "2.5.1"
        def logbackVersion = "1.2.3"
        def bouncyCastleVersion = "1.68"
        def mockitoVersion = "3.11.2"
        versionMapping = [
                "org.inferred:freebuilder"                                      : "2.6.1",
                "com.google.code.findbugs:jsr305"                               : "3.0.2",
                "com.google.guava:guava"                                        : "29.0-jre",
                "org.apache.commons:commons-lang3"                              : "3.0",
                "org.slf4j:slf4j-api"                                           : slf4jVersion,
                "org.slf4j:slf4j-simple"                                        : slf4jVersion,
                "org.freemarker:freemarker"                                     : "2.3.9",
                "info.picocli:picocli"                                          : "4.5.0",
                "commons-io:commons-io"                                         : "2.4",
                "commons-codec:commons-codec"                                   : "1.13",
                "org.apache.commons:commons-exec"                               : "1.3",
                "org.apache.commons:commons-compress"                           : "1.20",
                "mysql:mysql-connector-java"                                    : "8.0.21",
                "com.h2database:h2"                                             : "1.4.195",
                "com.fasterxml.jackson.core:jackson-core"                       : jacksonVersion,
                "com.fasterxml.jackson.core:jackson-databind"                   : jacksonVersion,
                "com.fasterxml.jackson.core:jackson-annotations"                : jacksonVersion,
                "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"      : jacksonVersion,
                "com.fasterxml.jackson.dataformat:jackson-dataformat-properties": jacksonVersion,
                "ch.qos.logback:logback-core"                                   : logbackVersion,
                "ch.qos.logback:logback-classic"                                : logbackVersion,
                "com.squareup.okhttp3:okhttp"                                   : "4.9.1",
                "io.grpc:grpc-api"                                              : grpcVersion,
                "io.grpc:grpc-core"                                             : grpcVersion,
                "io.grpc:grpc-netty-shaded"                                     : grpcVersion,
                "io.grpc:grpc-protobuf"                                         : grpcVersion,
                "io.grpc:grpc-stub"                                             : grpcVersion,
                "io.grpc:grpc-testing"                                          : grpcVersion,
                "io.grpc:protoc-gen-grpc-java"                                  : grpcVersion,
                "com.google.protobuf:protoc"                                    : "3.12.0",
                "org.apache.tomcat:annotations-api"                             : "6.0.53",
                "com.github.docker-java:docker-java-core"                       : dockerJavaVersion,
                "com.github.docker-java:docker-java-transport-httpclient5"      : dockerJavaVersion,
                "org.junit.jupiter:junit-jupiter-api"                           : junitJupiterVersion,
                "org.junit.jupiter:junit-jupiter-engine"                        : junitJupiterVersion,
                "org.apache.sshd:sshd-core"                                     : minaSshVersion,
                "org.apache.sshd:sshd-scp"                                      : minaSshVersion,
                "org.bouncycastle:bcprov-jdk15on"                               : bouncyCastleVersion,
                "org.bouncycastle:bcpkix-jdk15on"                               : bouncyCastleVersion,
                "org.springdoc:springdoc-openapi-ui"                            : "1.5.2",
                "io.codearte.props2yaml:props2yaml"                             : "0.5",
                "org.mockito:mockito-core"                                      : mockitoVersion,
                "org.mockito:mockito-inline"                                    : mockitoVersion,
                "org.mockito:mockito-junit-jupiter"                             : mockitoVersion,
        ]
        def version = versionMapping[groupWithArtifact]
        if (!version) {
            throw new RuntimeException("version not found for \"${groupWithArtifact}\"")
        }
        return version
    }
    jarDependency = { group, artifact ->
        String groupWithArtifact = "${group}:${artifact}"
        return "${groupWithArtifact}:${ext.jarDependencyVersion(groupWithArtifact)}"
    }
    simpleTemplateEngine = new SimpleTemplateEngine()
}
